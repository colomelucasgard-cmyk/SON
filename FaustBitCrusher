import("stdfaust.lib");

nBits = hslider("Bits", 16, 1, 16, 0.1); 
ds_factor = hslider("Downsample", 1, 1, 50, 1);

filter_active = checkbox("AntiAlias"); 


cutoff = (ma.SR / (2 * ds_factor))*0.8;


anti_alias_filter = _ <: (filter_active, _, fi.lowpass(2, cutoff)) : select2;


q_levels = 2.0^nBits - 1.0;
quantizer(x) = round(x * q_levels) / q_levels;

clock = ba.pulse(ds_factor); 
downsampler(x) = ba.latch(clock, x); 

original_effect = anti_alias_filter : anti_alias_filter : downsampler : quantizer;


//Vinyle

vinyl_active = checkbox("Vinyl_Mode"); 
wear = hslider("Vinyl_Wear", 0, 0, 1, 0.01);//ie "age" du vinyle

// wow and flutter, ie  simulation des variations de vitesse du moteur
wf_mix = wear * 0.003; // variation hauteur (pitch) avec 3ms
lfo_wow = os.osc(0.5); // Oscillation lente d'un sinus 
lfo_flutter = no.noise : fi.lowpass(1, 10); //bruit blanc + passe bas 10Hz pour éviter trop de grésillement

//retard
total_mod = (lfo_wow + lfo_flutter) * wf_mix;
time_delay = (0.01 + total_mod) * ma.SR; //0.01 = éviter un retard causal puisque viariations de 3 ms 
wow_flutter_effect = de.fdelay(2000, time_delay);

// bruit
hiss_vol = wear * 0.02; // Volume du souffle
hiss = no.pink_noise * hiss_vol;

// Craquements
// On génère du bruit, on prend la valeur absolue, et on garde seulement les pics extrêmes
//seuil_craquement = 1.0 - (wear * 0.002); // Le seuil baisse quand l'usure augmente
//trigger = no.noise : abs : >(seuil_craquement);
//son_craque = trigger * no.noise * 0.5; // On multiplie par du bruit pour varier le timbre du clic


// Courbe RIAA simplifiée : Moins d'aigus quand le disque est vieux
cutoff_age = 18000 - (wear * 12000); // Passe de 18kHz (neuf) à 6kHz (vieux)
vinyl_filter = fi.highpass(1, 40) : fi.lowpass(2, cutoff_age);


saturation = ma.tanh(_ * (1 + (wear * 0.5))); //Soft clipping en arrondissant notre signal avec tanh 


// --- D. Assemblage de la chaîne Vinyle ---
vinyl_chain = _ <: 
    // Chemin 1 : Le signal audio traité (Wow -> Sat -> Filtre)
    (wow_flutter_effect : saturation : vinyl_filter),
    // Chemin 2 : Les bruits (Souffle + Craquements)
    (hiss)
    :> + ; // On mélange le tout

// Sélecteur Final : Si "Vinyl_Mode" est coché, on applique l'effet, sinon on passe outre
vinyl_stage = _ <: (vinyl_active, _, vinyl_chain) : select2;



process = original_effect : vinyl_stage;
